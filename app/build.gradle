apply plugin: 'com.android.application'

repositories {
    maven { url  "http://dl.bintray.com/dasar/maven" }
}


android {
    compileSdkVersion project.sdkVersion
    buildToolsVersion project.buildToolsVersion
    defaultConfig {
        applicationId "org.horaapps.leafpic"
        minSdkVersion 19
        targetSdkVersion project.sdkVersion
        versionName "v0.6-beta-2"
        versionCode 14
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        abortOnError false
    }

    dexOptions {
        jumboMode = true
    }

    // This is handled for you by the 2.0+ Gradle Plugin
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "LeafPic"
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "LeafPic (debug)"
        }
    }

    flavorDimensions "default"

    productFlavors {
        noGPlay {
            dimension "default"
        }

        withGPlay {
            dimension "default"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    
    // google & support
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:palette-v7:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"
    compile 'com.google.android.exoplayer:exoplayer:r2.0.4'

    // utils
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.koushikdutta.ion:ion:2.1.7'
    compile 'com.github.Commit451:bypasses:1.0.4'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    compile 'com.drewnoakes:metadata-extractor:2.9.1'
    compile "com.orhanobut:hawk:2.0.1"

    // rxJava
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //it is recommended to keep the same version of rxAndroid
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    // icons
    compile 'com.mikepenz:iconics-core:2.9.1@aar'
    compile "com.mikepenz:iconics-views:2.9.1@aar"
    compile 'com.mikepenz:google-material-typeface:3.0.1.1.original@aar'
    compile 'com.mikepenz:community-material-typeface:1.9.32.2@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.7.0.1@aar'

    // ui
    compile 'uz.shift:colorpicker:0.5@aar'
    compile 'com.github.jetradarmobile:desertplaceholder:1.1.1'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.yalantis:ucrop:1.5.0'
    compile 'com.github.chrisbanes:PhotoView:2.1.3'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.6.0'
    compile 'jp.wasabeef:recyclerview-animators:2.2.6'

    compile project(':liz')
    // debug Only
    //debugCompile project(':inappstoragereader')

    // TODO check them out
    compile 'com.turingtechnologies.materialscrollbar:lib:10.0.3'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.0'
}

Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('KEY_ALIAS') && props.containsKey('PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
